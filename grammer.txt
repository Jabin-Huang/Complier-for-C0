/*
    //e表示空字
    //函数（块）定义
    program -> process program | e
    process -> type id(parameter) block
    parameter -> type id ids | e
    ids -> , type id ids | e
    block -> {decls stmts}

    //变量、数组定义
    decls -> decl decls | e　
    decl -> type ID IDs ;
    type -> BASIC
    IDs  -> ,ID IDs | e
    ID -> id | id dims
    dims -> [NUM] dims | e

    //语句序列
    stmts -> stmt stmts　|e
    stmt ->  loc = exprEntry;
           | id(exprList);
           | if (exprEntry) stmt
           | if (exprEntry) stmt else stmt
           | while (exprEntry) stmt
           | do stmt while(exprEntry);
           | break;
           | block

    //左值
    loc -> id | id offset
    offset -> [NUM] offset | e

    //表达式（右值）
    exprEntry -> join || exprEntry
                | join
    join -> equality && join
          | equality
    equality ->  rel == equality
               | rel != equality
               | rel
    rel -> expr < expr
         | expr <= expr
         | expr >= expr
         | expr > expr
         | expr
    expr -> term + expr
          | term - expr
          | term
    term -> unary * term
          | unary / term
          | unary
    unary -> !unary
           | -unary
           | factor
    factor ->id(exprList)
             | (exprEntry)
             | loc
             | NUM
             | REAL
             | TRUE
             | FALSE

    //调用函数时的参数列表
    exprList -> exprEntry, exprlist
               | exprEntry
               | e
 */